# Maintainer: Wolfyxon <wolfyxon@gmail.com>

pkgname=lover-git
pkgdesc="Love2D build system and runner written in Rust"
url="https://github.com/Wolfyxon/lover"
license=("LGPL-2.1")
pkgrel=1
pkgver=2.0.r6.g2c1127f
depends=("openssl" "xz" "bzip2" "zstd" "gcc-libs" "glibc")
makedepends=("cargo" "git")
arch=("x86_64")
options=(!debug !lto)
source=("$pkgname::git+$url.git")
sha256sums=("SKIP")

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    
    cd $srcdir/$pkgname
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    
    cd $srcdir/$pkgname
    cargo build --frozen --release --all-features
}

package() {
	install -Dm755 $srcdir/$pkgname/target/release/lover $pkgdir/usr/bin/lover
}
