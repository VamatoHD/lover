# Maintainer: Wolfyxon <wolfyxon@gmail.com>

pkgname=lover-git
pkgdesc="Love2D build system and runner written in Rust"
url="https://github.com/Wolfyxon/lover"
pkgrel=1
pkgver=2.0.r5.g40518d5
depends=("openssl" "xz" "bzip2" "zstd" "gcc-libs" "glibc")
makedepends=("cargo" "git")
arch=("x86_64")
options=(!debug !lto)
source=("lover-git::git+$url.git")
sha256sums=("SKIP")

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    
    cd $srcdir/$pkgname
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
    git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    
    cd $srcdir/$pkgname
    cargo build --frozen --release --all-features
}

package() {
	install -Dm755 $srcdir/$pkgname/target/release/lover $pkgdir/usr/bin/lover
}
